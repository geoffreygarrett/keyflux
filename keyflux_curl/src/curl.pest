ws = _{ " " | "\t" }
newline = _{ "\r" | "\n" }
slash = _{ "\\" ~ ws* ~ newline+ }

single_quoted = _{ "'" ~ single_quoted_inner ~ "'" }
single_quoted_inner = { (!"'" ~ ANY)* }
double_quoted = _{ "\"" ~ double_quoted_inner ~ "\"" }
double_quoted_inner = { (!"\"" ~ ANY)* }

none_ws = { (!ws ~ ANY)+ }
query_param = @{ none_ws ~ "=" ~ none_ws }
url_plain = @{ "http" ~ "s"? ~ "://" ~ none_ws ~ ("?" ~ query_param ~ ("&" ~ query_param)*)? }
url = { single_quoted | double_quoted | url_plain }
option = _{ method_option | header_option | location_option | body_option | auth_option | form_option | compressed_option | get_option }

method_option = _{ ("-X" | "--request") ~ ws+ ~ method }
method = @{ none_ws }

header_option = _{ ("-H" | "--header") ~ ws+ ~ header }
header = { single_quoted | double_quoted | none_ws }

location_option = _{ ("-L" | "--location") ~ ws+ ~ location }
location = { single_quoted | double_quoted | none_ws }

body_option = _{ ("-d" | "--data") ~ ws+ ~ body }
body = { single_quoted | double_quoted | none_ws }

auth_option = _{ "-u" ~ ws+ ~ auth }
auth = { single_quoted | double_quoted | none_ws }

form_option = _{ ("-F" | "--form") ~ ws+ ~ form_data }
form_data = { single_quoted | double_quoted | none_ws }

compressed_option = _{ "--compressed" }
get_option = _{ "-G" }

wss = _{ ws* ~ slash* ~ ws* }

input = _{
    SOI ~ "curl" ~ wss ~ (option ~ wss )* ~ url? ~ (wss ~ option ~ wss)* ~ ws* ~ EOI
}
